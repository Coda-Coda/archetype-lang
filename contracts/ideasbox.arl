archetype ideasbox

constant admin : address = @tz1ZAQXACaEqryobpBoLbJUc2DjG5ZzrhARu
constant expiration : date = 2020-07-22

asset idea {
    id      : int;
    desc    : string;
    nbvotes : int = 0;
}

asset voter {
    addr : address;
    remaining : int = 5;
}

asset winner {
    wid : int;
}

entry register (a_voter : address) {
    called by admin
    effect { voter.add({addr = a_voter}) }
}

entry add_idea(description : string) {
    called by admin
    effect { idea.add({ id = idea.count(); desc = description}) }
}

entry vote(n : int, weight : int) {
    require {
        r1 : voter.contains(caller);
        r2 : now <= expiration;
    }
    effect {
       if voter[caller].remaining >= weight then (
            voter[caller].remaining -= weight;
            idea[n].nbvotes += weight
        )
    }
}

entry finalize() {
    called by admin
    require { r3 : now > expiration }
    effect {
        for i in idea.select(the.nbvotes >= 5).sort(desc(nbvotes)).head(3) do
            winner.add({wid = i})
        done
    }
}

specification {
    s1 : idea.sum(nbvotes) = voter.count() * 5 - voter.sum(remaining)
}
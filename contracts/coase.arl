archetype coase

asset card_pattern identified by card_pattern_id {
  card_pattern_id: int;
  coefficient: tez;
  quantity: int;
} shadow {
  sellcount: int = 0;
} with {
  cp1: card_pattern_id >= 0;
  cp2: quantity >= 0;
}

asset card identified by card_id {
  card_id: int;
  card_owner: address;
  card_pattern_card: pkey of card_pattern;
} with {
  c1: card_id >= 0;
}

variable next_id : int = 0
with {
  n1: next_id >= 0;
  (* n2: forall c in card, next_id <> c.card_id *)
}

action transfer_single (card_to_transfer : pkey of card, destination : address) {
  require {
    ts1: caller = card[card_to_transfer].card_owner;
  }
  effect {
    card.update(card_to_transfer, {card_owner = destination})
  }
}

action sell_single (card_to_sell : pkey of card) {
  specification {
    shadow effect {
      var k = card[card_to_sell].card_pattern_card;
      card_pattern.update(k, {sellcount += 1})
    }
  }
  require {
    ss1: caller = card[card_to_sell].card_owner;
  }
  effect {
    var card_pattern_key : int = card[card_to_sell].card_pattern_card;
    card_pattern.update(card_pattern_key, {quantity -= 1});
    card.remove(card_to_sell);
    var price : tez = card_pattern[card_pattern_key].quantity * card_pattern[card_pattern_key].coefficient;
    var receiver = caller;
    transfer price to receiver
  }
}

action buy_single (card_to_buy : pkey of card_pattern) {
  specification {
    postcondition bs1 {
      card.count() = before.card.count() + 1
    }
    postcondition bs2 {
      balance = before.balance + card_pattern[card_to_buy].quantity * card_pattern[card_to_buy].coefficient
    }
    postcondition bs3 {
      card.select(card_owner = caller).count() >= 1
    }
  }
  accept transfer
  effect {
    var price : tez = (card_pattern[card_to_buy].quantity + 1) * card_pattern[card_to_buy].coefficient;
    failif (price > transferred);
    card_pattern.update(card_to_buy, {quantity += 1});
    card.add({card_id = next_id; card_owner = caller; card_pattern_card = card_to_buy});
    next_id += 1
  }
}

specification {
  g1: balance = card_pattern.sum(((quantity * (quantity + 1)) / 2 + sellcount) * coefficient)
}

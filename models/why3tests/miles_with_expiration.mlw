module Miles

  use int.Int
  use mach.int.UInt32
  use ref.Ref
  use array.Array

  use cml.Types
  use cml.Contract
  use cml.Asset

  (* Mile *********************************************************************)

  type mile = {
     mile_id        : int;
     amount         : int;
     expiration     : date;
  } invariant { amount > 0 }

  function mile_key (m : mile) : int = m.mile_id
  function mile_sort (m : mile) : date = m.expiration

  function get_amount (c:array mile) (i : int) : int = let mile = c[i] in
                                                        mile.amount
  (* declare mile as an Asset *)
  clone cml.Asset as Mile with type      key   = int,
                               type      sort  = date,
                               type      asset = mile,
                               function  akey  = mile_key,
                               function  asort = mile_sort,
                               predicate kle   = (<=)

  (* Owner ********************************************************************)

  type owner = {
     addr    : address;
     miles   : Mile.collection;
  }

  function owner_key (o : owner) : int = o.addr

  (* declare mile as an Asset *)
  clone cml.Asset as Owner with type      key   = int,
                                type      sort  = int,
                                type      asset = owner,
                                function  akey  = owner_key,
                                function  asort = owner_key,
                                predicate kle   = (<=)

  (* Storage *****************************************************************)

  type storage = {
    mutable mile_coll   : Mile.collection;
    mutable owner_coll  : Owner.collection;
  }

  function is_mile (m : mile) (s : storage) : bool  = Mile.mem m s.mile_coll
  function is_owner (o : owner) (s : storage) : bool = Owner.mem o s.owner_coll

  function get_owner (addr : address) (s : storage) : owner = Owner.get addr s.owner_coll

  let ghost set_miles (o : owner) (c : Mile.collection) (s : storage) : unit =
    let new_o = {
      addr  = o.addr;
      miles = c;
    } in
    s.owner_coll <- Owner.add new_o s.owner_coll

  let ghost set_amount (m : mile) (a : int) (s : storage) : unit
  requires { a > 0 }
  = let new_m = {
      mile_id    = mile_key m;
      amount     = a;
      expiration = m.expiration;
    } in
    s.mile_coll <- Mile.add new_m s.mile_coll

  (* partition property *)
  axiom miles_partition : forall s:storage, m:mile.
           is_mile m s <-> (exists o:owner. is_owner o s /\ Mile.mem m o.miles)

  (* Transactions  ***********************************************************)

  function filter_expiration (d : date) (m : mile) : bool = m.expiration > d

  let ghost consume (e : env) (s : storage) (owner : address) (nbmiles : int) : transfers
  requires { nbmiles > 0 }
  requires {
     let o = get_owner owner s in
     let l = Mile.when (filter_expiration (now e)) o.miles in
     Mile.sum l get_amount 0 (length l) >= nbmiles
  }

  ensures { forall m:mile.
     Mile.mem m (Mile.diff (old s.mile_coll) s.mile_coll) -> m.expiration > (now e)
  }

  = let remainder = ref nbmiles in
    let o = get_owner owner s in
    let l = Mile.when (filter_expiration (now e)) o.miles in
    try
       for i = 0 to length l - 1 do
       invariant { forall m:mile.
     Mile.mem m (Mile.diff (old s.mile_coll) s.mile_coll) -> m.expiration > (now e) }
       (*invariant { !remainder = nbmiles - (Mile.sum l get_amount 0 i) }*)
          let m = l[i] in
          if m.amount > !remainder
          then
             (remainder := 0;
              set_amount m (m.amount - !remainder) s;
              raise Break)
          else if m.amount = !remainder
          then
             (remainder := 0;
              set_miles o (Mile.remove m o.miles) s;
              raise Break)
          else
             (remainder := !remainder - m.amount;
              set_miles o (Mile.remove m o.miles) s)
       done
    with Break -> ()
    end;
    (*assert { !remainder = 0 };*)
    empty

end